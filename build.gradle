plugins {
    id 'java'
    id "io.github.goooler.shadow" version "8.1.7"
}

group = 'com.github.lukesky19'
version = '1.0.2'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }
    maven {
        name = "PlaceholderAPI Repo"
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT"
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
    compileOnly 'me.clip:placeholderapi:2.11.6'
    implementation "net.kyori:adventure-api:4.17.0"
    implementation "net.kyori:adventure-text-minimessage:4.17.0"
    implementation "org.spongepowered:configurate-yaml:4.1.2"
    implementation "org.bstats:bstats-bukkit:3.0.2"
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveClassifier.set(null)
    relocate('org.spongepowered.configurate', 'com.github.lukesky19.skyshop.libs.configurate')
    relocate('org.bstats', 'com.github.lukesky19.skyshop.libs.bstats')
    minimize()
    manifest {
        attributes("paperweight-mappings-namespace": "mojang")
    }
}

build.dependsOn shadowJar